<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">
<html lang="en">
<!-- $Id: grammar.src,v 2.36 2004-01-19 16:58:43 bbos Exp $ -->
<HEAD>
<meta http-equiv="Content-Type" content="text/html; charset=ISO-8859-1">
<TITLE>Grammar of CSS&nbsp;2.1</TITLE>
</HEAD>
<BODY>
<H1>Grammar of CSS&nbsp;2.1</H1>
<P><em>This appendix is normative.</em>

<P>The grammar below defines the syntax of CSS&nbsp;2.1. It is in some sense,
however, a superset of CSS&nbsp;2.1 as this specification imposes additional
semantic constraints not expressed in this grammar. A conforming UA
must also adhere to the <a href="./syndata.html#syntax">
forward-compatible parsing rules</a>, the <a
href="./about.html#property-defs">property and value notation</a>,
and the unit notation. In
addition, the document language may impose restrictions, e.g. HTML
imposes restrictions on the possible values of the "class" attribute.

<!-- IJ: There used to be a link to "unit notation", but I couldn't
find anything in the spec that corresponded. -->

<h2>Grammar</h2>

<P> The grammar below is <span class="index-inst"
title="LALR(1)">LALR(1)</span> (but note that most UA's should not use it
directly, since it doesn't express the <a
href="syndata.html#parsing-errors">parsing conventions</a>, only the
CSS&nbsp;2.1 syntax). The format of the productions is optimized for human
consumption and some shorthand notation beyond Yacc (see [[YACC]]) is
used:</P>

<ul>
<li><strong>*</strong>: 0 or more
<li><strong>+</strong>: 1 or more
<li><strong>?</strong>: 0 or 1 
<li><strong>|</strong>: separates alternatives 
<li><strong>[&nbsp;]</strong>: grouping
</ul>

<p>The productions are:

<pre>
stylesheet
  : [ CHARSET_SYM S* STRING S* ';' ]?
    [S|CDO|CDC]* [ import [S|CDO|CDC]* ]*
    [ [ ruleset | media | page ] [S|CDO|CDC]* ]*
  ;
import
  : IMPORT_SYM S*
    [STRING|URI] S* [ medium [ COMMA S* medium]* ]? ';' S*
  ;
media
  : MEDIA_SYM S* medium [ COMMA S* medium ]* LBRACE S* ruleset* '}' S*
  ;
medium
  : IDENT S*
  ;
page
  : PAGE_SYM S* pseudo_page? S*
    LBRACE S* declaration [ ';' S* declaration ]* '}' S*
  ;
pseudo_page
  : ':' IDENT
  ;
operator
  : '/' S* | COMMA S* | /* empty */
  ;
combinator
  : PLUS S*
  | GREATER S*
  | S+
  ;
unary_operator
  : '-' | PLUS
  ;
property
  : IDENT S*
  ;
ruleset
  : selector [ COMMA S* selector ]*
    LBRACE S* declaration [ ';' S* declaration ]* '}' S*
  ;
<span class="index-inst" title="selector">selector</span>
  : simple_selector [ combinator simple_selector ]*
  ;
simple_selector
  : element_name [ HASH | class | attrib | pseudo ]*
  | [ HASH | class | attrib | pseudo ]+
  ;
class
  : '.' IDENT
  ;
element_name
  : IDENT | '*'
  ;
attrib
  : '[' S* IDENT S* [ [ '=' | INCLUDES | DASHMATCH ] S*
    [ IDENT | STRING ] S* ]? ']'
  ;
pseudo
  : ':' [ IDENT | FUNCTION S* IDENT? S* ')' ]
  ;
declaration
  : property ':' S* expr prio?
  | /* empty */
  ;
prio
  : IMPORTANT_SYM S*
  ;
expr
  : term [ operator term ]*
  ;
term
  : unary_operator?
    [ NUMBER S* | PERCENTAGE S* | LENGTH S* | EMS S* | EXS S* | ANGLE S* |
      TIME S* | FREQ S* | function ]
  | STRING S* | IDENT S* | URI S* | hexcolor
  ;
function
  : FUNCTION S* expr ')' S*
  ;
/*
 * There is a constraint on the <span class="index-inst" title="color">color</span> that it must
 * have either 3 or 6 hex-digits (i.e., [0-9a-fA-F])
 * after the "#"; e.g., "#000" is OK, but "#abcd" is not.
 */
hexcolor
  : HASH S*
  ;
</pre>

<h2>Lexical scanner</h2>

<p> The following is the <span class="index-def"
title="tokenizer">tokenizer</span>, written in Flex (see [[FLEX]])
notation. The tokenizer is case-insensitive.

<p>The two occurrences of "\377" represent the highest character
number that current versions of Flex can deal with (decimal 255). They
should be read as "\4177777" (decimal 1114111), which is the highest
possible code point in <span class="index-inst"
title="unicode">Unicode</span>/<span class="index-inst"
title="iso-10646">ISO-10646</span>.

<!-- ignored whitespace after Unicode escape, is it OK? -->
<pre>
%option case-insensitive

h		[0-9a-f]
nonascii	[\200-\377]
unicode		\\{h}{1,6}(\r\n|[ \t\r\n\f])?
escape		{unicode}|\\[ -~\200-\377]
nmstart		[_a-z]|{nonascii}|{escape}
nmchar		[_a-zA-Z0-9-]|{nonascii}|{escape}
string1		\"([\t !#$%&amp;(-~]|\\{nl}|\'|{nonascii}|{escape})*\"
string2		\'([\t !#$%&amp;(-~]|\\{nl}|\"|{nonascii}|{escape})*\'

ident		{nmstart}{nmchar}*
name		{nmchar}+
num		[0-9]+|[0-9]*"."[0-9]+
string		{string1}|{string2}
url		([!#$%&amp;*-~]|{nonascii}|{escape})*
s		[ \t\r\n\f]
w		{s}*
nl		\n|\r\n|\r|\f
range		\?{1,6}|{h}(\?{0,5}|{h}(\?{0,4}|{h}(\?{0,3}|{h}(\?{0,2}|{h}(\??|{h})))))

%%

{s}+			{return S;}

\/\*[^*]*\*+([^/*][^*]*\*+)*\/	/* ignore comments */

"&lt;!--"		{return CDO;}
"-->"			{return CDC;}
"~="			{return INCLUDES;}
"|="			{return DASHMATCH;}

{w}"{"			{return LBRACE;}
{w}"+"			{return PLUS;}
{w}">"			{return GREATER;}
{w}","			{return COMMA;}

{string}		{return STRING;}

{ident}			{return IDENT;}

"#"{name}		{return HASH;}

"@import"		{return IMPORT_SYM;}
"@page"			{return PAGE_SYM;}
"@media"		{return MEDIA_SYM;}
"@charset"		{return CHARSET_SYM;}

"!"{w}"important"	{return IMPORTANT_SYM;}

{num}em			{return EMS;}
{num}ex			{return EXS;}
{num}px			{return LENGTH;}
{num}cm			{return LENGTH;}
{num}mm			{return LENGTH;}
{num}in			{return LENGTH;}
{num}pt			{return LENGTH;}
{num}pc			{return LENGTH;}
{num}deg		{return ANGLE;}
{num}rad		{return ANGLE;}
{num}grad		{return ANGLE;}
{num}ms			{return TIME;}
{num}s			{return TIME;}
{num}Hz			{return FREQ;}
{num}kHz		{return FREQ;}
{num}{ident}		{return DIMEN;}
{num}%			{return PERCENTAGE;}
{num}			{return NUMBER;}

"url("{w}{string}{w}")"	{return URI;}
"url("{w}{url}{w}")"	{return URI;}
{ident}"("		{return FUNCTION;}

.			{return *yytext;}
</pre>

<h2><a name="tokenizer-diffs">Comparison of tokenization in CSS&nbsp;2.1 and
CSS1</a></h2>

<p>There are some differences in the syntax specified in the CSS1
recommendation ([[-CSS1]]), and the one above. Most of these are due
to new tokens in CSS2 that didn't exist in CSS1. Others are because
the grammar has been rewritten to be more readable. However, there are
some incompatible changes, that were felt to be errors in the CSS1
syntax. They are explained below.

<ul>
<li>CSS1 style sheets could only be in 1-byte-per-character
encodings, such as ASCII and ISO-8859-1. CSS&nbsp;2.1 has no such
limitation. In practice, there was little difficulty in extrapolating
the CSS1 tokenizer, and some UAs have accepted 2-byte encodings.

<li>CSS1 only allowed four hex-digits after the backslash (\) to refer
to Unicode characters, CSS2 <a
href="syndata.html#escaped-characters">allows six</a>. Furthermore,
CSS2 allows a whitespace character to delimit the escape
sequence. E.g., according to CSS1, the string "\abcdef" has 3 letters
(\abcd, e, and f), according to CSS2 it has only one (\abcdef).

<li>The tab character (ASCII 9) was not allowed in strings. However,
since strings in CSS1 were only used for font names and for URLs, the
only way this can lead to incompatibility between CSS1 and CSS2 is if
a style sheet contains a font family that has a tab in its name.

<li>Similarly, newlines (<a href="syndata.html#strings">escaped with a
backslash</a>) were not allowed in strings in CSS1.

<li>CSS2 parses a number immediately followed by an identifier as a
DIMEN token (i.e., an unknown unit), CSS1 parsed it as a number and an
identifier. That means that in CSS1, the declaration 'font:
10pt/1.2serif' was correct, as was 'font: 10pt/12pt serif'; in CSS2, a
space is required before "serif". (Some UAs accepted the first
example, but not the second.)

<li>In CSS1, a class name could start with a digit (".55ft"), unless
it was a dimension (".55in"). In CSS2, such classes are parsed as
unknown dimensions (to allow for future additions of new units). To
make ".55ft" a valid class, CSS2 requires the first digit to be
escaped (".\35 5ft")
</ul>


</body>
</html>
<!-- Keep this comment at the end of the file
Local variables:
mode: sgml
sgml-declaration:"~/SGML/HTML4.decl"
sgml-default-doctype-name:"html"
sgml-minimize-attributes:t
sgml-nofill-elements:("pre" "style" "br")
sgml-live-element-indicator:t
End:
-->
